# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Dan Ziemba <zman0900@gmail.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 <bender02@gmx.com>

pkgname=lib32-nettle4
pkgver=2.7.1
pkgrel=4
pkgdesc='A low-level cryptographic library'
arch=('x86_64')
url='http://www.lysator.liu.se/~nisse/nettle/'
license=('GPL2')
depends=('lib32-gmp')
makedepends=('gcc-multilib')
source=("ftp://ftp.gnu.org/gnu/nettle/nettle-${pkgver}.tar.gz")
sha256sums=('bc71ebd43435537d767799e414fce88e521b7278d48c860651216e1fc6555b40')

build() {
  cd nettle-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --with-include-path='/usr/lib32/gmp' \
    --enable-shared
  make
}

package() {
  cd nettle-${pkgver}

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/include/nettle{,4}
  rm -rf "${pkgdir}"/usr/{bin,lib32/{lib{hogweed,nettle}.{a,so},pkgconfig},share}
}

# vim ts=2 sw=2 et:
