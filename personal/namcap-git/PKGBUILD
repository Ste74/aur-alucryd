# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: sudokode <sudokode@gmail.com>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Jesse Young <jesse.young@gmail.com>

pkgname=namcap-git
pkgver=3.2.1.r35.228172a
pkgrel=1
pkgdesc='A Pacman package analyzer'
arch=('any')
url='http://projects.archlinux.org/namcap.git/'
license=('GPL')
depends=('binutils' 'elfutils' 'licenses' 'pyalpm-git' 'python'
         'python-pyelftools')
makedepends=('git' 'python-distribute')
provides=('namcap')
conflicts=('namcap')
source=('git+http://projects.archlinux.org/git/namcap.git')
sha256sums=('SKIP')

pkgver() {
  cd namcap

  git describe --always | sed 's/-/.r/; s/-g/./'
}

check() {
  cd namcap

  export PARSE_PKGBUILD_PATH="${srcdir}/namcap"
  export PATH="${srcdir}/namcap:$PATH"

  python setup.py test
}

package() {
  cd namcap

  python ./setup.py install --root="$pkgdir"
}

# vim: ts=2 sw=2 et:
